plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'maven-publish'
}

group 'dev.josantonius.minecraft.messaging'
version 'v1.0.0'

repositories {
    mavenCentral()
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'
    implementation "net.kyori:adventure-platform-bukkit:4.3.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

jar {
    exclude('.github/**')
    exclude('.gitignore')
    exclude('.CHANGELOG.md')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'dev.josantonius.minecraft.messaging'
            artifactId = 'library'
            version = 'v1.0.0'
            pom {
                name = 'Minecraft Messaging Library'
                description = 'Easily send messages to players on a Minecraft server running PaperMC, Bukkit, or Spigot'
                url = 'https://github.com/josantonius/minecraft-messaging'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/josantonius/minecraft-messaging/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'josantonius'
                        name = 'Josantonius'
                        email = 'hello@josantonius.dev'
                    }
                }
            }

            from components.java
        }
    }
}
